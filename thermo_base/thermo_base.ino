#include <ESP8266WiFi.h>
#include <ESP8266WebServerSecure.h>
#include <ESP8266mDNS.h>
#include <SoftwareSerial.h>

#define min(a,b) ((a<b)?a:b)
#define max(a,b) ((a>b)?a:b)

#include "LEDDisplayDriver.h"
#include "config.h"
#include "data.h"

#ifndef STASSID
#define STASSID ""
#define STAPSK  ""
#endif

const char* ssid = STASSID;
const char* password = STAPSK;

ESP8266WebServerSecure server(443);

// The certificate is stored in PMEM
static const uint8_t x509[] PROGMEM = {
  0x30, 0x82, 0x01, 0x3d, 0x30, 0x81, 0xe8, 0x02, 0x09, 0x00, 0xfe, 0x56,
  0x46, 0xf2, 0x78, 0xc6, 0x51, 0x17, 0x30, 0x0d, 0x06, 0x09, 0x2a, 0x86,
  0x48, 0x86, 0xf7, 0x0d, 0x01, 0x01, 0x0b, 0x05, 0x00, 0x30, 0x26, 0x31,
  0x10, 0x30, 0x0e, 0x06, 0x03, 0x55, 0x04, 0x0a, 0x0c, 0x07, 0x45, 0x53,
  0x50, 0x38, 0x32, 0x36, 0x36, 0x31, 0x12, 0x30, 0x10, 0x06, 0x03, 0x55,
  0x04, 0x03, 0x0c, 0x09, 0x31, 0x32, 0x37, 0x2e, 0x30, 0x2e, 0x30, 0x2e,
  0x31, 0x30, 0x1e, 0x17, 0x0d, 0x31, 0x37, 0x30, 0x33, 0x31, 0x38, 0x31,
  0x34, 0x34, 0x39, 0x31, 0x38, 0x5a, 0x17, 0x0d, 0x33, 0x30, 0x31, 0x31,
  0x32, 0x35, 0x31, 0x34, 0x34, 0x39, 0x31, 0x38, 0x5a, 0x30, 0x26, 0x31,
  0x10, 0x30, 0x0e, 0x06, 0x03, 0x55, 0x04, 0x0a, 0x0c, 0x07, 0x45, 0x53,
  0x50, 0x38, 0x32, 0x36, 0x36, 0x31, 0x12, 0x30, 0x10, 0x06, 0x03, 0x55,
  0x04, 0x03, 0x0c, 0x09, 0x31, 0x32, 0x37, 0x2e, 0x30, 0x2e, 0x30, 0x2e,
  0x31, 0x30, 0x5c, 0x30, 0x0d, 0x06, 0x09, 0x2a, 0x86, 0x48, 0x86, 0xf7,
  0x0d, 0x01, 0x01, 0x01, 0x05, 0x00, 0x03, 0x4b, 0x00, 0x30, 0x48, 0x02,
  0x41, 0x00, 0xc6, 0x72, 0x6c, 0x12, 0xe1, 0x20, 0x4d, 0x10, 0x0c, 0xf7,
  0x3a, 0x2a, 0x5a, 0x49, 0xe2, 0x2d, 0xc9, 0x7a, 0x63, 0x1d, 0xef, 0xc6,
  0xbb, 0xa3, 0xd6, 0x6f, 0x59, 0xcb, 0xd5, 0xf6, 0xbe, 0x34, 0x83, 0x33,
  0x50, 0x80, 0xec, 0x49, 0x63, 0xbf, 0xee, 0x59, 0x94, 0x67, 0x8b, 0x8d,
  0x81, 0x85, 0x23, 0x24, 0x06, 0x52, 0x76, 0x55, 0x9d, 0x18, 0x09, 0xb3,
  0x3c, 0x10, 0x40, 0x05, 0x01, 0xf3, 0x02, 0x03, 0x01, 0x00, 0x01, 0x30,
  0x0d, 0x06, 0x09, 0x2a, 0x86, 0x48, 0x86, 0xf7, 0x0d, 0x01, 0x01, 0x0b,
  0x05, 0x00, 0x03, 0x41, 0x00, 0x69, 0xdc, 0x6c, 0x9b, 0xa7, 0x62, 0x57,
  0x7e, 0x03, 0x01, 0x45, 0xad, 0x9a, 0x83, 0x90, 0x3a, 0xe7, 0xdf, 0xe8,
  0x8f, 0x46, 0x00, 0xd3, 0x5f, 0x2b, 0x0a, 0xde, 0x92, 0x1b, 0xc5, 0x04,
  0xc5, 0xc0, 0x76, 0xf4, 0xf6, 0x08, 0x36, 0x97, 0x27, 0x82, 0xf1, 0x60,
  0x76, 0xc2, 0xcd, 0x67, 0x6c, 0x4b, 0x6c, 0xca, 0xfd, 0x97, 0xfd, 0x33,
  0x9e, 0x12, 0x67, 0x6b, 0x98, 0x7e, 0xd5, 0x80, 0x8f
};

// And so is the key.  These could also be in DRAM
static const uint8_t rsakey[] PROGMEM = {
  0x30, 0x82, 0x01, 0x3a, 0x02, 0x01, 0x00, 0x02, 0x41, 0x00, 0xc6, 0x72,
  0x6c, 0x12, 0xe1, 0x20, 0x4d, 0x10, 0x0c, 0xf7, 0x3a, 0x2a, 0x5a, 0x49,
  0xe2, 0x2d, 0xc9, 0x7a, 0x63, 0x1d, 0xef, 0xc6, 0xbb, 0xa3, 0xd6, 0x6f,
  0x59, 0xcb, 0xd5, 0xf6, 0xbe, 0x34, 0x83, 0x33, 0x50, 0x80, 0xec, 0x49,
  0x63, 0xbf, 0xee, 0x59, 0x94, 0x67, 0x8b, 0x8d, 0x81, 0x85, 0x23, 0x24,
  0x06, 0x52, 0x76, 0x55, 0x9d, 0x18, 0x09, 0xb3, 0x3c, 0x10, 0x40, 0x05,
  0x01, 0xf3, 0x02, 0x03, 0x01, 0x00, 0x01, 0x02, 0x40, 0x35, 0x0b, 0x74,
  0xd3, 0xff, 0x15, 0x51, 0x44, 0x0f, 0x13, 0x2e, 0x9b, 0x0f, 0x93, 0x5c,
  0x3f, 0xfc, 0xf1, 0x17, 0xf9, 0x72, 0x94, 0x5e, 0xa7, 0xc6, 0xb3, 0xf0,
  0xfe, 0xc9, 0x6c, 0xb1, 0x1e, 0x83, 0xb3, 0xc6, 0x45, 0x3a, 0x25, 0x60,
  0x7c, 0x3d, 0x92, 0x7d, 0x53, 0xec, 0x49, 0x8d, 0xb5, 0x45, 0x10, 0x99,
  0x9b, 0xc6, 0x22, 0x3a, 0x68, 0xc7, 0x13, 0x4e, 0xb6, 0x04, 0x61, 0x21,
  0x01, 0x02, 0x21, 0x00, 0xea, 0x8c, 0x21, 0xd4, 0x7f, 0x3f, 0xb6, 0x91,
  0xfa, 0xf8, 0xb9, 0x2d, 0xcb, 0x36, 0x36, 0x02, 0x5f, 0xf0, 0x0c, 0x6e,
  0x87, 0xaa, 0x5c, 0x14, 0xf6, 0x56, 0x8e, 0x12, 0x92, 0x25, 0xde, 0xb3,
  0x02, 0x21, 0x00, 0xd8, 0x99, 0x01, 0xf1, 0x04, 0x0b, 0x98, 0xa3, 0x71,
  0x56, 0x1d, 0xea, 0x6f, 0x45, 0xd1, 0x36, 0x70, 0x76, 0x8b, 0xab, 0x69,
  0x30, 0x58, 0x9c, 0xe0, 0x45, 0x97, 0xe7, 0xb6, 0xb5, 0xef, 0xc1, 0x02,
  0x21, 0x00, 0xa2, 0x01, 0x06, 0xc0, 0xf2, 0xdf, 0xbc, 0x28, 0x1a, 0xb4,
  0xbf, 0x9b, 0x5c, 0xd8, 0x65, 0xf7, 0xbf, 0xf2, 0x5b, 0x73, 0xe0, 0xeb,
  0x0f, 0xcd, 0x3e, 0xd5, 0x4c, 0x2e, 0x91, 0x99, 0xec, 0xb7, 0x02, 0x20,
  0x4b, 0x9d, 0x46, 0xd7, 0x3c, 0x01, 0x4c, 0x5d, 0x2a, 0xb0, 0xd4, 0xaa,
  0xc6, 0x03, 0xca, 0xa0, 0xc5, 0xac, 0x2c, 0xe0, 0x3f, 0x4d, 0x98, 0x71,
  0xd3, 0xbd, 0x97, 0xe5, 0x55, 0x9c, 0xb8, 0x41, 0x02, 0x20, 0x02, 0x42,
  0x9f, 0xd1, 0x06, 0x35, 0x3b, 0x42, 0xf5, 0x64, 0xaf, 0x6d, 0xbf, 0xcd,
  0x2c, 0x3a, 0xcd, 0x0a, 0x9a, 0x4d, 0x7c, 0xad, 0x29, 0xd6, 0x36, 0x57,
  0xd5, 0xdf, 0x34, 0xeb, 0x26, 0x03
};

#define NUM_DIGITS 24
/* For Uno:
  const byte dataPin = A0;
  const byte clockPin = A1;
  const byte loadPin = A2;
  const byte intPin = 2;
*/
/* For ESP8266MOD */
const byte dataPin = D1;
const byte loadPin = D2;
const byte clockPin = D3;

LEDDisplayDriver display(dataPin, clockPin, loadPin, true, NUM_DIGITS);

#define BAUD_RATE 57600
#define SERIAL_RX_PIN D7
SoftwareSerial mySerial(SERIAL_RX_PIN, D8); // RX, TX

#define NUM_REMOTES 7

// 0=basement, 1=aaron, 2=garage, 3=Office, 4=Florida, 5=here, 6=master
struct RemoteMetaData metadata[NUM_REMOTES];

void setup() {
  Serial.begin(115200);
  Serial.println("Hello thermo base");

  for (int i = 0; i < NUM_REMOTES; ++i) {
    metadata[i].missed = 0;
    metadata[i].when = 0;
    metadata[i].maxTemp = -100;
    metadata[i].minTemp = 212;
  }

  display.begin();
  display.setBrightness(1);
  display.showText("Connecting");

  int dot = 0;
  WiFi.begin(ssid, password);
  // Wait for connection
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    display.showText(".", dot++, 1);
  }
  // Allows us to find this at https://base.local
  MDNS.begin("base");

  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());

  mySerial.begin(BAUD_RATE);

  server.getServer().setServerKeyAndCert_P(rsakey, sizeof(rsakey), x509, sizeof(x509));
  server.on("/", handleRoot);
  server.begin();
}

void serialHandler() {
  if (mySerial.available() >= RX_DATA_SIZE) {
    struct Data data;
    byte *rawData = (byte*)&data;
    for (int i = 0; i < RX_DATA_SIZE; ++i) {
      rawData[i] = mySerial.read();
    }
    // Validate the checksum
    short calcChecksum = getChecksum(&data);
    if (data.checksum != calcChecksum) {
      display.showText("CHECKSUM ERROR");
      Serial.print("CHECKSUM MISMATCH; got "); Serial.print(data.checksum);
      Serial.print(" but calculated as "); Serial.println(calcChecksum);
      char tmp[32];
      snprintf_P(tmp,
                 sizeof(tmp),
                 PSTR("%c%c: %d F %d V (%d)"),
                 data.counter,
                 data.id,
                 (short)data.tempF,
                 data.voltage,
                 data.checksum);
      Serial.print("Data was ~:"); Serial.println(tmp);
      // Flush the input
      Serial.print("Flushing: skipping: ");
      while (mySerial.available() > 0) {
        Serial.print(mySerial.read(), HEX); Serial.print(" ");
      }
      Serial.println();
      return;
    }

    int slot = 0;
    char code = data.id;
    switch (code) {
      case 'b':
      case 'B':
        slot = 0;
        break;
      case 'a':
      case 'A':
        slot = 1;
        break;
      case 'g':
      case 'G':
        slot = 2;
        break;
      case 'o':
      case 'O':
        slot = 3;
        break;
      case 'f':
      case 'F':
        slot = 4;
        break;
      case 'h':
      case 'H':
        slot = 5;
        break;
      case 'm':
      case 'M':
        slot = 6;
        break;
      default:
        char temp[32];
        snprintf_P(temp,
                   sizeof(temp),
                   PSTR("%c%c: %d F %d V"),
                   data.counter,
                   data.id,
                   (short)data.tempF,
                   data.voltage);
        Serial.print("UNKNOWN SOURCE; Received ~: ");
        Serial.println(temp);
        return;
    }
    snprintf_P(metadata[slot].summary,
               sizeof(metadata[slot].summary),
               PSTR("%c%c: %d F %d V"),
               data.counter,
               data.id,
               (short)data.tempF,
               data.voltage);
    Serial.print("Received ~: ");
    Serial.print(metadata[slot].summary);

    if (data.tempF < -10 || data.tempF > 110) {
      display.showTextScroll("Skipping weird/invalid data");
      Serial.println("Skipping weird/invalid data");
      return;
    }
    metadata[slot].maxTemp = max(metadata[slot].maxTemp, data.tempF);
    metadata[slot].minTemp = min(metadata[slot].minTemp, data.tempF);
    Serial.print(". Min: "); Serial.print(metadata[slot].minTemp);
    Serial.print(", Max: "); Serial.print(metadata[slot].maxTemp);
    Serial.println();
    if (metadata[slot].when != 0) {
      // See if we missed one
      if (!(data.counter == 'A' && metadata[slot].data.counter == 'Z')) {
        // Not wraparound
        if (data.counter != metadata[slot].data.counter) {
          // Not a dupe
          if (data.counter != metadata[slot].data.counter + 1) {
            metadata[slot].missed++;
            Serial.print("Missed "); Serial.print(metadata[slot].missed); Serial.print(" from "); Serial.println(data.id);
          }
        }
      }
    }
    metadata[slot].when = millis();
    memcpy(&(metadata[slot].data), &data, sizeof(struct Data));
  }
}

short getChecksum(struct Data * remoteData) {
  short checksum = 0;
  byte *raw = (byte *)remoteData;
  // Skip the actual checksum bytes
  for (int i = 0; i < RX_DATA_SIZE - 2; ++i) {
    checksum += raw[i] * 3 * (i + 1);
  }
  return checksum;
}

void handleRoot() {
  Serial.println("Handling /");
  long now = millis();
  char buffer[500];
  sprintf(buffer,
          "Basement: %s at %d. Min %d Max %d\n   Aaron: %s at %d. Min %d Max %d\n  Garage: %s at %d. Min %d Max %d\n  Office: %s at %d. Min %d Max %d\n Florida: %s at %d. Min %d Max %d\n    Here: %s at %d. Min %d Max %d\n  Master: %s at %d. Min %d Max %d",
          metadata[0].summary, (now - metadata[0].when) / 1000, (int)metadata[0].minTemp, (int)metadata[0].maxTemp,
          metadata[1].summary, (now - metadata[1].when) / 1000, (int)metadata[1].minTemp, (int)metadata[1].maxTemp,
          metadata[2].summary, (now - metadata[2].when) / 1000, (int)metadata[2].minTemp, (int)metadata[2].maxTemp,
          metadata[3].summary, (now - metadata[3].when) / 1000, (int)metadata[3].minTemp, (int)metadata[3].maxTemp,
          metadata[4].summary, (now - metadata[4].when) / 1000, (int)metadata[4].minTemp, (int)metadata[4].maxTemp,
          metadata[5].summary, (now - metadata[5].when) / 1000, (int)metadata[5].minTemp, (int)metadata[5].maxTemp,
          metadata[6].summary, (now - metadata[6].when) / 1000, (int)metadata[6].minTemp, (int)metadata[6].maxTemp
         );
  Serial.println(buffer);
  server.send(200, "text/plain", buffer);
}

int source = 0;
long lastShown = 0;

void loop() {
  serialHandler();

  server.handleClient();

  long now = millis();
  if (lastShown == 0 || now - lastShown > 4000) {
    // yes this fails for rollover
    lastShown = now;
    long secondsSince = (now - metadata[source].when) / 1000;
    switch (source) {
      case 0:
        display.showText("Basement", 0, 8);
        break;
      case 1:
        display.showText("Aaron's", 0, 8);
        break;
      case 2:
        display.showText("Garage", 0, 8);
        break;
      case 3:
        display.showText("Office", 0, 8);
        break;
      case 4:
        display.showText("Florida", 0, 8);
        break;
      case 5:
        display.showText("Here", 0, 8);
        break;
      case 6:
        display.showText("Our br", 0, 8);
        break;
    }
    if (secondsSince < 9999 && metadata[source].when != 0) {
      display.showNum(secondsSince, 8, 8);
      display.showNum1decimal(metadata[source].data.tempF, 8, 3);
      display.showText("F", 11, 1);
      char third[12];
      sprintf(third, "Lo%dHi%d",
              (int)(metadata[source].minTemp + .5),
              (int)(metadata[source].maxTemp + .5));
      display.showText(third, 16, 8);
    } else {
      display.showText("No data", 8, 16);
    }

    source++;
    if (source == NUM_REMOTES) source = 0;

    MDNS.update();
  }
}
